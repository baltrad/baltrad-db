# Copyright 2010 Estonian Meteorological and Hydrological Institute
# 
# This file is part of baltrad-db.
# 
# baltrad-db is free software: you can redistribute it and/or modify
# it under the terms of the GNU Lesser General Public License as
# published by the Free Software Foundation, either version 3 of the
# License, or (at your option) any later version.
# 
# baltrad-db is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU Lesser General Public License for more details.
# 
# You should have received a copy of the GNU Lesser General Public License
# along with baltrad-db. If not, see <http://www.gnu.org/licenses/>.

Import("env", "conf")

if not conf.has_test_deps():
    print "Test dependencies not met, skipping test/brfc/SConstruct"
    Return()

env.AppendUnique(CPPPATH=["${gtest_include_dir}"])
env.AppendUnique(LIBPATH=["${gtest_lib_dir}"])
env.AppendENVPath("LD_LIBRARY_PATH", env.Dir("${gtest_lib_dir}").abspath)

def strlit(var):
    return "\"%s\"" % var

config_values = {
    "%test_dsn_count%": len(env["test_db_dsns"]),
    "%test_dsns%": ",\n".join(map(strlit, env["test_db_dsns"])),
    "%test_schema_dir%": strlit(env.Dir("#schema").abspath)
}

config_hpp = env.Substfile("config.hpp.in", SUBST_DICT=config_values)
config_cpp = env.Substfile("config.cpp.in", SUBST_DICT=config_values)
env.AppendUnique(CPPPATH=Dir("."))

env.Depends([config_hpp, config_cpp], env.Value(env["test_db_dsns"]))

sources = Glob("*.cpp") + Glob("oh5/*.cpp") + Glob("rdb/*.cpp")

# remove -pedantic flag (errors in gmock headers)
env["CCFLAGS"].remove("-pedantic")

objects = env.SharedObject(sources)
env.Depends(objects, [config_hpp])

runner = env.Program("#test/runner", objects,
            LIBS=["gtest", "gmock", "brfc", "boost_filesystem"])
env.Alias("build-gtest-tests", runner)


run_gtest_tests = env.Command("run_gtest_tests", "#test/runner",
                              "$SOURCE "
                              "--gtest_output=xml:test/reports/gtest.xml")
env.Alias("run-gtest-tests", run_gtest_tests)


# vim:filetype=python:et:ts=4:sw=4:
