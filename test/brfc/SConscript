# Copyright 2010 Estonian Meteorological and Hydrological Institute
# 
# This file is part of baltrad-db.
# 
# baltrad-db is free software: you can redistribute it and/or modify
# it under the terms of the GNU Lesser General Public License as
# published by the Free Software Foundation, either version 3 of the
# License, or (at your option) any later version.
# 
# baltrad-db is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU Lesser General Public License for more details.
# 
# You should have received a copy of the GNU Lesser General Public License
# along with baltrad-db.  If not, see <http://www.gnu.org/licenses/>.

Import("env")

def strlit(var):
    return "\"%s\"" % var

config_values = {
    "%test_dsn_count%": len(env["test_db_dsns"]),
    "%test_dsns%": ",\n".join(map(strlit, env["test_db_dsns"])),
    "%test_schema_dir%": strlit(env.Dir("#schema").abspath)
}

config_hpp = env.Substfile("config.hpp.in", SUBST_DICT=config_values)
config_cpp = env.Substfile("config.cpp.in", SUBST_DICT=config_values)
env.AppendUnique(CPPPATH=Dir("."))

env.Depends([config_hpp, config_cpp], env.Value(env["test_db_dsns"]))

sources = Glob("*.cpp") + Glob("oh5/*.cpp") + Glob("rdb/*.cpp")

# remove -pedantic flag if using older g++
cxxver = tuple(map(int, env["CXXVERSION"].split(".")))
if cxxver < (4, 3, 0):
    env["CCFLAGS"].remove("-pedantic")

confdeps = env.ConfHas("#gtest-confdeps", ["gtest", "gmock", "hlhdf"])

objects = env.SharedObject(sources)
env.Requires(objects, confdeps)
env.Depends(objects, [config_hpp])

runner = env.Program("#test/runner", objects,
            LIBS=["gtest", "gmock", "brfc", "hlhdf"])

# vim:filetype=python:et:ts=4:sw=4:
